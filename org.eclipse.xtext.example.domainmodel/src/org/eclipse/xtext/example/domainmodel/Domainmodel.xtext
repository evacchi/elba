/*******************************************************************************
 * Copyright (c) 2009 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.example.domainmodel.Domainmodel with org.eclipse.xtext.xbase.Xbase

generate domainmodel "http://www.xtext.org/example/Domainmodel"

DomainModel:
	importSection=XImportSection?
	elements+=AbstractElement*;

AbstractElement:
	PackageDeclaration | Entity | TypeClass | TypeClassInstance;

PackageDeclaration:
	'package' name=QualifiedName '{'
	elements+=AbstractElement*
	'}';

Entity:
	'data' 'class' name=ValidID ('extends' superType=JvmParameterizedTypeReference)? '{'
	features+=Feature*
	'}';

Feature:
	Property | Operation;

Property:
	type=JvmTypeReference name=ValidID;

TypeClass:
	'typeclass' name=QualifiedName '{'
		members+=TypeClassMember*
	'}'
;

TypeClassMember:
	type=JvmTypeReference name=ValidID 
		'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')'
;

TypeClassInstance:
	'impl' typeClassName=[TypeClass] 'for' name=QualifiedName '{'
		members+=TypeClassInstanceMember
	'}'
;

TypeClassInstanceMember:
	 type=JvmTypeReference name=QualifiedName
		'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' 
			body=XBlockExpression
;


Operation:
	'op' name=ValidID '(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' (':'
	type=JvmTypeReference)?
	body=XBlockExpression;


